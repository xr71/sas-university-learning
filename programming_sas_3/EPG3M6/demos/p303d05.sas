****************************************************************;
*  Processing Two-Dimensional Arrays: Part 2                   *;
****************************************************************;
 
****************************************************************;
*  Demo                                                        *;
*     Note: The debugger portion of this demo must be          *;
*     performed in Enterprise Guide.                           *;
*  1) Notice the ARRAY statement that creates the              *;
*     two-dimensional array Avg. This array is defined using 5 *;
*     rows of year values and 12 columns of month values. The  *;
*     array creates 60 new columns, Avg1 through Avg60. For    *;
*     simplicity, the second SET statement contains a WHERE=   *;
*     data set option that limits the daily average            *;
*     temperatures to the 15th day of the month.               *;
*  2) In Enterprise Guide, use the DATA Step Debugger.         *;
*     a) Click the Toggle DATA Step Debugger toolbar button to *;
*        enable debugging in the program. Click the Debugger   *;
*        icon next to the DATA statement. The DATA Step        *;
*        Debugger window appears. Notice that at the beginning *;
*        of execution there are 60 average columns with        *;
*        missing values.                                       *;
*     b) Click the Step execution to the next line toolbar     *;
*        button to execute through the statements that are     *;
*        loading the two-dimensional array. Observe that the   *;
*        values of Yr, Temp1 through Temp12, Month, and Avg1   *;
*        through Avg60 change.                                 *;
*     c) To speed through the loading of the array, put a      *;
*        watch on the Avg60 column. Click the Start/continue   *;
*        debugger execution toolbar button. This continues     *;
*        the execution until the Avg60 column has been         *;
*        changed.                                              *;
*     d) Click the Step execution to the next line toolbar     *;
*        button. Observe that the values of Date,              *;
*        TempDailyAvg, Y, M, TempMonthlyAvg, and Difference    *;
*        change.                                               *;
*     e) Continue clicking the Step execution to the next line *;
*        toolbar button until _N_ changes to 2 (the second     *;
*        iteration of the DATA step).                          *;
*     f) Notice the values of Avg1 through Avg60 are reset to  *;
*        missing. Because these values are assigned by the 12  *;
*        assignment statements, they are reset to missing at   *;
*        the beginning of each iteration.                      *;
*     g) Close the DATA Step Debugger window.                  *;
*  3) Add _temporary_ to the two-dimensional ARRAY statement,  *;
*     which retains the loaded values and eliminates the       *;
*     columns from the output table.                           *;
*       array Avg[2013:2017,12] _temporary_;                   *;
*  4) Modify the IF-THEN block to eliminate the repetitive     *;
*     assignment statements.                                   *;
*       if _N_=1 then do Yr=2013 to 2017;                      *;
*          set pg3.weather_dublin_monthly5yr                   *;
*             (keep=Temp1-Temp12);                             *;
*          array T[12] Temp1-Temp12;                           *;
*          do Month=1 to 12;                                   *;
*             Avg[Yr,Month]=T[Month];                          *;
*          end;                                                *;
*       end;                                                   *;
*  5) Run the DATA step. Verify that the output table contains *;
*     the desired output.                                      *;
****************************************************************;
  
data work.DublinDaily;
    array Avg[2013:2017,12];  
    if _N_=1 then do Yr=2013 to 2017;
       set pg3.weather_dublin_monthly5yr(keep=Temp1-Temp12);
       Avg[Yr,1]=Temp1;
       Avg[Yr,2]=Temp2;
       Avg[Yr,3]=Temp3;
       Avg[Yr,4]=Temp4;
       Avg[Yr,5]=Temp5;
       Avg[Yr,6]=Temp6;
       Avg[Yr,7]=Temp7;
       Avg[Yr,8]=Temp8;
       Avg[Yr,9]=Temp9;
       Avg[Yr,10]=Temp10;
       Avg[Yr,11]=Temp11;
       Avg[Yr,12]=Temp12;
    end;
    set pg3.weather_dublin_daily5yr(where=(day(Date)=15)
                                    keep=Date TempDailyAvg);
    Y=year(Date);
    M=month(Date);
    TempMonthlyAvg=avg[Y,M];
    Difference=TempDailyAvg-TempMonthlyAvg;
    keep Date TempDailyAvg TempMonthlyAvg Difference; 
run;
